# :q/usr/bin/env python
import os, os.path as path

Import('build')

build.add_thirdparty(build.env, 'poco')

env = build.env.Clone()
env.Append(LIBPATH=[path.join(build.dir, 'modules', 'ti.App')])
env.Append(LIBS=['kroll', 'tiappmodule','javascriptmodule'])
env.Append(CPPPATH = [build.kroll_include_dir, '%s/modules/javascript' % build.kroll_source_dir, '%s/modules/ti.App' % build.titanium_source_dir])

sources = Glob('*.cpp')
webkit_tp = path.join(build.third_party, 'webkit')
webkit_lib = path.join(webkit_tp, 'lib')
webkit_inc = path.join(webkit_tp, 'include')


if build.is_linux():
	sources = sources + Glob('gtk/*.cpp') + Glob('url/*.cpp')

	curl_tp = path.join(build.third_party, 'libcurl')
	curl_lib = path.join(curl_tp, 'lib')
	curl_inc = path.join(curl_tp, 'include')

	env.Append(LIBS=['webkit-1.0', 'curl'])
	env.Append(LIBPATH=[webkit_lib, curl_lib])
	env.Append(CPPPATH=[webkit_inc, curl_inc])
	env.ParseConfig('pkg-config --cflags --libs gtk+-2.0 gdk-2.0')

elif build.is_win32():
	sources = sources + Glob('win32/*.cpp') + Glob('win32/*.cc') + Glob('url/*.cpp')
	sources += Glob('binding/kjs*.cpp')
	env.Append(LIBPATH=[webkit_lib])
	env.Append(CPPPATH=[webkit_inc, path.join(webkit_inc, 'webkit'), path.join(webkit_inc, 'JavaScriptCore'), path.join(build.third_party, 'icu', 'include'), '#kroll'])
	env.Append(LIBS=['WebKitGUID','WebKit','kernel32','shell32','user32','ole32','comctl32','shlwapi', 'oleaut32', 'icuuc','pthreadVC2','libcurl_imp','khost'])
	env.Append(CCFLAGS=['/MD'])
	env.Append(LINKFLAGS=['/NODEFAULTLIB:LIBCMT.lib', '/LTCG', '/INCREMENTAL:NO'])

elif build.is_osx():
	env.Append(FRAMEWORKPATH=[webkit_tp])
	env.Append(FRAMEWORKS=['Cocoa','Carbon','WebKit','JavaScriptCore'])
	sources = sources + Glob('osx/*.mm') + Glob('osx/*.cpp')
	env.Append(CCFLAGS=['-x', 'objective-c++'])
	
s = env.SharedLibrary(target = build.dir + '/tiuimodule',
                        source = sources)

if build.is_osx():
	for i in Glob('osx/*.xib'):
		base = path.splitext(path.basename(str(i)))[0]
		env.AddPostAction(s, 'ibtool --errors --warnings --notices --output-format human-readable-text --compile '+build.dir + '/'+ base+'.nib '+str(i))

if build.is_win32():
	manifest1 = os.path.join(build.dir, 'tiuimodule.embed.manifest')
	manifest2 = os.path.join(build.dir, 'tiuimodule.dll.manifest')
	env.AddPostAction(s, 'mt.exe /nologo /manifest "' + manifest2 + '" /tlb:"' + path.join(webkit_lib, 'WebKit.tlb') + '" /dll:bin/WebKit /out:"'+manifest1+'"')
	#env.AddPostAction(s, 'mt.exe /nologo /tlb:' + path.join(win32_webkit, 'lib', 'WebKit.tlb') + ' /dll:WebKit /outputresource:'+build.dir+'/tikernel.exe;1')
	#env.AddPostAction(s, 'mt.exe -nologo -manifest "'+manifest1+'" -outputresource:"'+build.dir+'/kkernel.exe;1"')
	#env.AddPostAction(s, 'mt.exe -nologo -manifest "'+manifest1+'" -outputresource:'+build.dir+'/tiuimodule.dll;2')
